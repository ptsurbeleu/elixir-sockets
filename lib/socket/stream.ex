#            DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
#                    Version 2, December 2004
#
#            DO WHAT THE FUCK YOU WANT TO PUBLIC LICENSE
#   TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION
#
#  0. You just DO WHAT THE FUCK YOU WANT TO.

defprotocol Socket.Stream.Protocol do
  @doc """
  Send data through the socket.
  """
  @spec send(t, iodata) :: :ok | {:error, term}
  def send(self, data)

  @doc """
  Send a file through the socket, using non-copying operations where available.
  """
  @spec file(t, String.t(), Keyword.t()) :: :ok | {:error, term}
  def file(self, path, options)

  @doc """
  Receive data from the socket compatible with the packet type.
  """
  @spec recv(t) :: {:ok, term} | {:error, term}
  def recv(self)

  @doc """
  Receive data from the socket with the given length or options.
  """
  @spec recv(t, non_neg_integer | Keyword.t()) :: {:ok, term} | {:error, term}
  def recv(self, length_or_options)

  @doc """
  Receive data from the socket with the given length and options.
  """
  @spec recv(t, non_neg_integer, Keyword.t()) :: {:ok, term} | {:error, term}
  def recv(self, length, options)

  @doc """
  Shutdown the socket in the given mode, either `:both`, `:read`, or `:write`.
  """
  @spec shutdown(t, :both | :read | :write) :: :ok | {:error, term}
  def shutdown(self, how)

  @doc """
  Close the socket.
  """
  @spec close(t) :: :ok | {:error, term}
  def close(self)
end

defmodule Socket.Stream do
  @moduledoc """
  Unified interface for stream-based sockets (TCP, SSL, Port).

  Delegates to `Socket.Stream.Protocol` and provides bang (`!`) variants of all
  operations, plus `io/2,3` for streaming data from an IO device directly into a
  socket.
  """
  @type t :: Socket.Stream.Protocol.t()

  use Socket.Helpers
  import Kernel, except: [send: 2]

  defdelegate send(self, data), to: Socket.Stream.Protocol
  defbang(send(self, data), to: Socket.Stream.Protocol)

  defdelegate file(self, path, options), to: Socket.Stream.Protocol
  defbang(file(self, path, options), to: Socket.Stream.Protocol)

  defdelegate recv(self), to: Socket.Stream.Protocol
  defbang(recv(self), to: Socket.Stream.Protocol)
  defdelegate recv(self, length_or_options), to: Socket.Stream.Protocol
  defbang(recv(self, length_or_options), to: Socket.Stream.Protocol)
  defdelegate recv(self, length, options), to: Socket.Stream.Protocol
  defbang(recv(self, length, options), to: Socket.Stream.Protocol)

  defdelegate shutdown(self, how), to: Socket.Stream.Protocol
  defbang(shutdown(self, how), to: Socket.Stream.Protocol)

  defdelegate close(self), to: Socket.Stream.Protocol
  defbang(close(self), to: Socket.Stream.Protocol)

  @doc """
  Read from the IO device and send to the socket following the given options.

  ## Options

    - `:size` is the amount of bytes to read from the IO device, if omitted it
      will read until EOF
    - `:offset` is the amount of bytes to read from the IO device before
      starting to send what's being read
    - `:chunk_size` is the size of the chunks read from the IO device at a time

  """
  @spec io(t, :io.device()) :: :ok | {:error, term}
  @spec io(t, :io.device(), Keyword.t()) :: :ok | {:error, term}
  def io(self, io, options \\ []) do
    if offset = options[:offset] do
      case IO.binread(io, offset) do
        :eof ->
          :ok

        {:error, reason} ->
          {:error, reason}

        _ ->
          io(0, self, io, options[:size] || -1, options[:chunk_size] || 4096)
      end
    else
      io(0, self, io, options[:size] || -1, options[:chunk_size] || 4096)
    end
  end

  defp io(total, self, io, size, chunk_size) when size > 0 and total + chunk_size > size do
    case IO.binread(io, size - total) do
      :eof ->
        :ok

      {:error, reason} ->
        {:error, reason}

      data ->
        self |> send(data)
    end
  end

  defp io(total, self, io, size, chunk_size) do
    case IO.binread(io, chunk_size) do
      :eof ->
        :ok

      {:error, reason} ->
        {:error, reason}

      data ->
        self |> send(data)

        io(total + chunk_size, self, io, size, chunk_size)
    end
  end

  defbang(io(self, io))
  defbang(io(self, io, options))

  @doc false
  def file_emulation(self, path, options) do
    cond do
      options[:size] && options[:chunk_size] ->
        file_emulation(self, path, options[:offset] || 0, options[:size], options[:chunk_size])

      options[:size] ->
        file_emulation(self, path, options[:offset] || 0, options[:size], 4096)

      true ->
        file_emulation(self, path, 0, -1, 4096)
    end
  end

  defp file_emulation(self, path, offset, -1, chunk_size) when path |> is_binary do
    file_emulation(self, path, offset, File.stat!(path).size, chunk_size)
  end

  defp file_emulation(self, path, offset, size, chunk_size) when path |> is_binary do
    case File.open!(
           path,
           [:read],
           &io(self, &1, offset: offset, size: size, chunk_size: chunk_size)
         ) do
      {:ok, :ok} ->
        :ok

      {:ok, {:error, reason}} ->
        {:error, reason}

      {:error, reason} ->
        {:error, reason}
    end
  end
end

defimpl Socket.Stream.Protocol, for: Port do
  def send(self, data) do
    :gen_tcp.send(self, data)
  end

  def file(self, path, options \\ []) do
    cond do
      options[:size] && options[:chunk_size] ->
        :file.sendfile(path, self, options[:offset] || 0, options[:size],
          chunk_size: options[:chunk_size]
        )

      options[:size] ->
        :file.sendfile(path, self, options[:offset] || 0, options[:size], [])

      true ->
        :file.sendfile(path, self)
    end
  end

  def recv(self) do
    recv(self, 0, [])
  end

  def recv(self, length) when length |> is_integer do
    recv(self, length, [])
  end

  def recv(self, options) when options |> is_list do
    recv(self, 0, options)
  end

  def recv(self, length, options) do
    timeout = options[:timeout] || :infinity

    case :gen_tcp.recv(self, length, timeout) do
      {:ok, _} = ok ->
        ok

      {:error, :closed} ->
        {:ok, nil}

      {:error, reason} ->
        {:error, reason}
    end
  end

  def shutdown(self, how \\ :both) do
    :gen_tcp.shutdown(
      self,
      case how do
        :read -> :read
        :write -> :write
        :both -> :read_write
      end
    )
  end

  def close(self) do
    :gen_tcp.close(self)
  end
end

defimpl Socket.Stream.Protocol, for: Tuple do
  require Record

  def send(self, data) when self |> Record.is_record(:sslsocket) do
    :ssl.send(self, data)
  end

  def file(self, path, options \\ []) when self |> Record.is_record(:sslsocket) do
    Socket.Stream.file_emulation(self, path, options)
  end

  def recv(self) when self |> Record.is_record(:sslsocket) do
    recv(self, 0, [])
  end

  def recv(self, length) when self |> Record.is_record(:sslsocket) and length |> is_integer do
    recv(self, length, [])
  end

  def recv(self, options) when self |> Record.is_record(:sslsocket) and options |> is_list do
    recv(self, 0, options)
  end

  def recv(self, length, options) when self |> Record.is_record(:sslsocket) do
    timeout = options[:timeout] || :infinity

    case :ssl.recv(self, length, timeout) do
      {:ok, _} = ok ->
        ok

      {:error, :closed} ->
        {:ok, nil}

      {:error, reason} ->
        {:error, reason}
    end
  end

  def shutdown(self, how \\ :both) do
    :ssl.shutdown(
      self,
      case how do
        :read -> :read
        :write -> :write
        :both -> :read_write
      end
    )
  end

  def close(self) do
    :ssl.close(self)
  end
end

defimpl Socket.Stream.Protocol, for: Socket.Port do
  require Logger

  def send(%Socket.Port{port: port, owner: owner}, data) do
    Kernel.send(port, {owner, {:command, data}})
    :ok
  end

  def file(self, path, options \\ []) do
    Socket.Stream.file_emulation(self, path, options)
  end

  def recv(self) do
    recv(self, 0, [])
  end

  def recv(self, length) when length |> is_integer do
    recv(self, length, [])
  end

  def recv(self, options) when options |> is_list do
    recv(self, 0, options)
  end

  def recv(%Socket.Port{port: port}, _length, options) do
    # Figure out an actual timeout and do a short circuit
    # when the port has already exited
    {timeout, reply_state} =
      if Port.info(port) == nil,
        do: {0, :closed},
        else: {options[:timeout] || :infinity, :timeout}

    # NOTE: Always check mailbox for incoming messages that have been received
    # after the process has exited (eq. a race condition of shortlived port)
    receive do
      # Partial line message, eq. specified size limit reached before newline
      {^port, {:data, {:noeol, value}}} ->
        {:ok, value}

      # Complete line (incl. EOL) match
      {^port, {:data, {:eol, value}}} ->
        {:ok, value}

      # Raw stream data, no line framing (eq. line or fragment-based)
      {^port, {:data, value}} ->
        {:ok, value}

      # Port process exited; status is the OS exit code
      {^port, {:exit_status, status}} ->
        {:error, {:exit_status, status}}

      # Port was killed or crashed abnormally
      {:EXIT, ^port, reason} ->
        {:error, reason}
    after
      timeout ->
        {:error, reply_state}
    end
  end

  @spec shutdown(Socket.Port.t()) :: no_return()
  @spec shutdown(Socket.Port.t(), :read | :write | :both) :: no_return()
  def shutdown(_self, _how \\ :both) do
    raise "not implemented"
  end

  def close(self) do
    Port.close(self.port)
    :ok
  rescue
    e ->
      Logger.error(Exception.format(:error, e, __STACKTRACE__))
      {:error, Exception.message(e)}
  end
end
